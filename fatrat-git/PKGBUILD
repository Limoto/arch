# Contributor: Jakub Luzny <limoto94@gmail.com>

pkgname=fatrat-git
pkgver=20080720
pkgrel=1
pkgdesc='Qt4 (C++) based download manager with support for HTTP, FTP, SFTP, BitTorrent, rapidshare and more'
arch=(i686 x86_64)
url='http://fatrat.dolezel.info/'
license=('GPL')
makedepends=('cmake>=2.6.0')
depends=('qt>=4.4.0' 'rb_libtorrent>=0.13.1' 'curl' 'gloox>=0.9')
provides=('fatrat')
conflicts=('fatrat' 'fatrat-svn')
replaces=('fatrat-svn')
source=()
md5sums=()
install=fatrat.install

_gitroot="git://dolezel.info/fatrat.git"
_gitname="fatrat"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d "$srcdir/$_gitname" ] ; then
    cd $_gitname && git-pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -r "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$startdir/src/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # needed for rb_libtorrent 0.13.1
  sed -i 's/FIND_LIBRARY(LIBTORRENT_LIBRARY torrent)/FIND_LIBRARY(LIBTORRENT_LIBRARY torrent-rasterbar)/' "cmake_modules/libtorrentConfig.cmake" || return 1

  cmake . -DCMAKE_INSTALL_PREFIX='/usr' -DWITH_BITTORRENT=ON -DWITH_CURL=ON -DWITH_NLS=ON -DWITH_JABBER=ON -DWITH_DOCUMENTATION=ON -DWITH_WEBINTERFACE=ON
  make || return 1
  make DESTDIR="$pkgdir" install 

}

#category: network
# vim:set ts=2 sw=2 et:
